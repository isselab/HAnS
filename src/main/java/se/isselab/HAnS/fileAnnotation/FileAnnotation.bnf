/*
Copyright 2021 Herman Jansson & Johan Martinson

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/
{
  parserClass="se.isselab.HAnS.fileAnnotation.parser.FileAnnotationParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="FileAnnotation"
  psiImplClassSuffix="Impl"
  psiPackage="se.isselab.HAnS.fileAnnotation.psi"
  psiImplPackage="se.isselab.HAnS.fileAnnotation.psi.impl"

  elementTypeHolderClass="se.isselab.HAnS.fileAnnotation.psi.FileAnnotationTypes"
  elementTypeClass="se.isselab.HAnS.fileAnnotation.psi.FileAnnotationElementType"
  tokenTypeClass="se.isselab.HAnS.fileAnnotation.psi.FileAnnotationTokenType"

  psiImplUtilClass="se.isselab.HAnS.fileAnnotation.psi.impl.FileAnnotationPsiImplUtil"
}

fileAnnotationFile ::= (fileAnnotation NEWLINE*)*

fileAnnotation ::= fileReferences COLON? NEWLINE+ lpqReferences

fileReferences ::= (fileReference ((SPACE* CS)? SPACE* fileReference)* SPACE*)

fileReference ::= (CM fileName CM) | (fileName){
mixin="se.isselab.HAnS.referencing.impl.FeatureAnnotationNamedElementImpl"}

fileName ::= (STRING DOT STRING)|STRING

lpqReferences ::= (lpq ((SPACE* CS)? SPACE* lpq)* SPACE*)

//&begin[Referencing]
lpq ::= featureName (SEPARATOR featureName)* {
mixin="se.isselab.HAnS.referencing.impl.FeatureAnnotationNamedElementImpl"
implements="se.isselab.HAnS.referencing.FeatureAnnotationNamedElement"
methods=[getName setName getNameIdentifier]}
//&end[Referencing]

featureName ::= STRING